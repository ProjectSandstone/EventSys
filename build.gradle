buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

plugins {
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'com.github.johnrengelman.shadow' version '5.1.0'
}

group 'com.github.koresframework'
version '1.8.1'

// Programming Language
apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'
apply plugin: 'java'

// License
apply plugin: 'license'

// Shade
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
}

dependencies {
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Utilities
    implementation 'com.github.koresframework:Kores:4.0.0.54-beta'
    implementation 'com.github.koresframework.Kores-SourceWriter:Kores-SourceWriter:4.0.0.25-beta-source'
    implementation 'com.github.koresframework.Kores-BytecodeWriter:Kores-BytecodeWriter:4.0.0.39-beta-bytecode'
    implementation 'com.github.koresframework:Kores-Extra:1.3.8'
    implementation 'com.github.JonathanxD:JwIUtils:4.16.5-1'
    implementation "io.github.lukehutch:fast-classpath-scanner:3.1.15"

    testImplementation group: 'junit', name: 'junit', version: '4.12'
}

// Tasks
jar {
    from "$rootProject.rootDir/LICENSE"

    manifest {
        attributes 'Implementation-Title': baseName,
                'Implementation-Version': version
    }
}

shadowJar {
    classifier = 'shaded'
}

dokka {
    outputFormat = 'markdown'
    outputDirectory = javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: dokka) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
    archives shadowJar
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.freeCompilerArgs = ["-Xuse-experimental=kotlin.Experimental"]
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.freeCompilerArgs = ["-Xuse-experimental=kotlin.Experimental"]
}

test {
    useJUnit()
}

license {
    ext.name = project.name
    ext.description = project.description
    ext.organization = project.organization
    ext.url = project.url
    ext.year = Calendar.getInstance().get(Calendar.YEAR)

    exclude "**/*.info"
    exclude "**/*.md"
    exclude "res/**"
    exclude "src/main/resources/**"
    exclude "src/test/resources/**"

    header rootProject.file('LICENSE_HEADER')

    sourceSets = project.sourceSets

    ignoreFailures false
    strictCheck true

    mapping {
        java = 'SLASHSTAR_STYLE'
        kt = 'SLASHSTAR_STYLE'
    }
}

dokka {
    enabled = false
}